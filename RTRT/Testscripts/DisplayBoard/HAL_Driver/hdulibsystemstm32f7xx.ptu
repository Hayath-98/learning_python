HEADER hdulibsystemstm32f7xx,Source Code Ver :1.5,TestScript Ver : 1.4
--------------------------------------------------------------------------------
-- Author Name         : Samhitha Prasad L C
--
-- File Name           : hdulibsystemstm32f7xx.ptu
--
-- Test Creation Date  : 30.10.2017
--
-- Tested Functions    : SystemInit
--		                   
-- Version History     :
--
--  TEST-VERSION        TEST-DATE       TESTER NAME           MODIFICATIONS
--  ------------        ---------       -----------           -------------
--      1.1             30.10.2017      Samhitha Prasad L C   Created the ptu file.
--      1.2 			10.11.2017	    Samhitha Prasad L C	  Updated as per #PR100171
--      1.3 			15.11.2017	    Samhitha Prasad L C	  Updated as per #PR100171
--	  1.4			   03.10.2020          Harshitha R        Updated as per PR#100281	

--------------------------------------------------------------------------------
--
-- TESTED FILE INCLUDE DIRECTIVES 
--
--------------------------------------------------------------------------------*
#/*============================================================================*/
#/* Module specific includes                                                   */
#/*============================================================================*/

##include "hdulibcorecm7.h"          
##include "hdulibstm32f7xx.h"          
##include "hdulibbasictypes.h"          
##include "hdulibstm32f746xx.h"         
##include "hdulibsystemstm32f7xx.h" 
##include "hdulibstm32f7xxhalconf.h" 
--------------------------------------------------------------------------------
--
-- TESTED FILE DECLARATIONS 
--
--------------------------------------------------------------------------------

-- Declarations of the global variables of the tested file

	#T_UINT8 Vector_tab_sel;
	
-- Declaration of the temporary variable of the tested file
	
	--None
--------------------------------------------------------------------------------

BEGIN

	COMMENT Author Name      	: Samhitha Prasad L C
	COMMENT		
	COMMENT File Name        	: hdulibsystemstm32f7xx.ptu
	COMMENT
	COMMENT Tested Functions	: SystemInit
	COMMENT                       
	COMMENT
	COMMENT Assumptions and Constraints
	COMMENT
	COMMENT <AC>
	COMMENT
	COMMENT Assumption(s):														   
	COMMENT
	COMMENT None
	COMMENT
	COMMENT Constraint(s):
	COMMENT
	COMMENT 1. Header file "hdulibstm32f746xx.h" is modified for testing purpose. The following macro's are modified
	COMMENT for stubbing hardware address.
	COMMENT /*Commented for testing purpose*/
	COMMENT /*#define RCC                 ((T_RCC *) RCC_BASE)*/
	COMMENT
	COMMENT /* Modified macro by stubbing hardware address for testing purpose*/ 
	COMMENT
	COMMENT T_RCC     PTU_RCC;
	COMMENT #define RCC                 ((T_RCC *) &PTU_RCC)
	COMMENT
	COMMENT 2. Header file "hdulibcorecm7.h" is modified for testing purpose. The following macro's are modified
	COMMENT for stubbing hardware address.
	COMMENT
	COMMENT //*Commented for testing purpose */
	COMMENT  /*#define SCB                 ((T_SCB       *)     SCB_BASE      )*/  
	COMMENT		
	COMMENT /* Modified macro by stubbing hardware address for testing purpose*/ 
	COMMENT T_SCB PTU_SCB;
	COMMENT	#define SCB                 ((T_SCB       *)     &PTU_SCB      )
	COMMENT
	COMMENT 3. Following Test script related warnings are observed during Target Execution for volatile 
	COMMENT keyword member/variable:
	COMMENT warning #191-D: type qualifier is meaningless on cast type
	COMMENT
	COMMENT </AC>
	
	COMMENT Equivalence Class Description
	COMMENT <ECD>
	COMMENT
	COMMENT Valid Class(es)
	COMMENT
	COMMENT Class 1
	COMMENT In function 'SystemInit', vector table select is set to one
	COMMENT
	COMMENT Class 2
	COMMENT In function 'SystemInit', vector table select is not set to one
	COMMENT
	COMMENT Class 3
	COMMENT In function 'SystemInit', vector table select is set to two
	COMMENT
	COMMENT Class 4
	COMMENT In function 'SystemInit', vector table select is not set to two
	COMMENT
	COMMENT Class 5
	COMMENT In function 'SystemInit', vector table select is set to other than one or two
	COMMENT
	COMMENT Invalid class(es)
	COMMENT  None
	COMMENT </ECD>

--------------------------------------------------------------------------------
--
-- STUB DECLARATIONS OF THE TESTED OR INTEGRATED UNITS
--
--------------------------------------------------------------------------------

SIMUL

     DEFINE STUB filename_c

           -- No stubs

     END DEFINE

ELSE_SIMUL

    -- No action defined as of now if the module is not simulated

END SIMUL

--------------------------------------------------------------------------------
--
-- ENVIRONMENTS DECLARATIONS
--
--------------------------------------------------------------------------------
--IF HOST

-- No special things that have to be done on the host to run the test.

--ELSE

-- No action defined for the target execution as of now

--END IF
--------------------------------------------------------------------------------
--------------------------------------------------------------------------------
-- TESTED FILE ENVIRONNEMENT
--------------------------------------------------------------------------------

-- Environment of the file hdulibsystemstm32f7xx.c

ENVIRONMENT ENV_hdulibsystemstm32f7xx_c

  VAR Vector_tab_sel,		init = 0,		ev = init
  
END ENVIRONMENT -- ENV_hdulibsystemstm32f7xx_c

USE ENV_hdulibsystemstm32f7xx_c

--------------------------------------------------------------------------------
--
-- SERVICES DECLARATIONS
--
--------------------------------------------------------------------------------

--------------------------------------------------------------------------------
-- Declaration of the service SystemInit
--------------------------------------------------------------------------------
SERVICE SystemInit
SERVICE_TYPE extern


TEST 1
FAMILY nominal

	COMMENT Test Description
	COMMENT <TD>
	COMMENT	[hdulibsystemstm32f7xx-SystemInit-DB-LLR-008]
	COMMENT The function sets CPACR of SCB to result of bitwise OR of CPACR of SCB , THREE left shifted by product of 
	COMMENT	TEN and TWO and THREE left shifted by product of ELEVEN and TWO.
	COMMENT	Equivalence Class None
	COMMENT
	COMMENT [hdulibsystemstm32f7xx-SystemInit-DB-LLR-001]
	COMMENT	The function sets the HSION bit of CR register. 
	COMMENT Equivalence Class None
	COMMENT 
	COMMENT [hdulibsystemstm32f7xx-SystemInit-DB-LLR-002]
	COMMENT The function resets the CFGR register.
	COMMENT	Equivalence Class None
	COMMENT
	COMMENT [hdulibsystemstm32f7xx-SystemInit-DB-LLR-003]
	COMMENT The function resets HSEON, CSSON and PLLON bits of the CR register.
	COMMENT Equivalence Class None
	COMMENT
	COMMENT [hdulibsystemstm32f7xx-SystemInit-DB-LLR-004]
	COMMENT The function resets PLLCFGR register.
	COMMENT Equivalence Class None
	COMMENT
	COMMENT [hdulibsystemstm32f7xx-SystemInit-DB-LLR-005]
	COMMENT The function resets HSEBYP bit of CR register.
	COMMENT Equivalence Class None
	COMMENT
	COMMENT [hdulibsystemstm32f7xx-SystemInit-DB-LLR-006]
	COMMENT The function disables all the interrupts by resetting CIR register.
	COMMENT Equivalence Class None
	COMMENT
	COMMENT [hdulibsystemstm32f7xx-SystemInit-DB-LLR-009]
	COMMENT	The function sets VTOR of SCB to result of bitwise OR of FLASH_BASE and VECT_TAB_OFFSET_BOOT when 
	COMMENT Vector_tab_sel is equal to ONE.
	COMMENT Equivalence Class 1
	COMMENT	</TD>
	
    ELEMENT
    
    	COMMENT INPUT SECTION:
    		
			--Initializing vector table select value.
    		VAR Vector_tab_sel,					init = 1,						ev = init
    	
    	COMMENT INPUT/OUTPUT SECTION:
    	
    		-- None
    
    	COMMENT OUTPUT SECTION:
			
			--Sets CPACR of SCB value.
    		VAR  SCB->CPACR,					init = 0x100,					ev = 0xF00100
    		
			--Sets CR of RCC value.
    		VAR RCC->CR,						init= 0x298,					ev = 0x299
    		
			--Sets CFGR of RCC value.
    		VAR RCC->CFGR,						init = 0x123,					ev = HEX_ZERO
    		
			--Sets PLLCFGR of RCC value.
    		VAR RCC->PLLCFGR,					init = 0x444,					ev = HEX_PLLCFGR
    		
			--Sets CFGR of RCC value.
    		VAR RCC->CIR,						init = 0x444,					ev = HEX_ZERO
    		
			--Sets VTOR of SCB value.
    		VAR SCB->VTOR,						init = 0x0,						ev = 0x8000000
    	
    	-- Stub CallS
    	SIMUL
    		
			-- 	No Stubs
			
    	END SIMUL
    
    -- Calling the Unit under test
    #SystemInit();
    END ELEMENT

END TEST -- TEST 1

TEST 2
FAMILY nominal 

	COMMENT Test Description
	COMMENT <TD>
	COMMENT [hdulibsystemstm32f7xx-SystemInit-DB-LLR-009]
	COMMENT	The function does nothig when Vector_tab_sel is not equal to ONE.
	COMMENT Equivalence Class 2
	COMMENT
	COMMENT	[hdulibsystemstm32f7xx-SystemInit-DB-LLR-010]
	COMMENT The function sets VTOR of SCB to result of bitwise OR of FLASH_BASE and VECT_TAB_OFFSET_FLIGHT when 
	COMMENT Vector_tab_sel is equal to TWO.
	COMMENT Equivalence Class 3
	COMMENT	</TD>
	
    ELEMENT
    
    	COMMENT INPUT SECTION:
    		
			--Initializing vector table select value.
    		VAR	 Vector_tab_sel,				init = 2,						ev = init
    	
    	COMMENT INPUT/OUTPUT SECTION:
    	
    		-- None
    
    	COMMENT OUTPUT SECTION:
			
			--Sets CPACR of SCB value.	
    		VAR  SCB->CPACR,					init = 0x100,					ev = 0xF00100
    		
			--Sets CR of RCC value.
    		VAR RCC->CR,						init= 0x10,						ev = 0x11
    		
			--Sets CFGR of RCC value.
    		VAR RCC->CFGR,						init = 0x123,					ev = HEX_ZERO
    		
			--Sets PLLCFGR of RCC value.
    		VAR RCC->PLLCFGR,					init = 0x444,					ev = HEX_PLLCFGR
    		
			--Sets CFGR of RCC value.
    		VAR RCC->CIR,						init = 0x444,					ev = HEX_ZERO
    		
			--Sets VTOR of RCC value.
    		VAR SCB->VTOR,						init = 0x0,					ev = 0x80C0000
    		
    		-- Stub Calls
    		SIMUL
    		
    			-- No Stubs
    		
    		END SIMUL
    		
    -- Calling the Unit undet test
    #SystemInit();
    END ELEMENT

END TEST -- TEST 2

TEST 3
FAMILY nominal

	COMMENT Test Description
	COMMENT <TD>
	COMMENT	[hdulibsystemstm32f7xx-SystemInit-DB-LLR-010]
	COMMENT The function does nothing when vector table select is not equal to one and two 
	COMMENT Equivalence Class 4,5
	COMMENT	</TD>
	
    ELEMENT
    
    	COMMENT INPUT SECTION:
    		
    		-- Initializing vector table select value.
    		VAR Vector_tab_sel,					init = 10,						ev = init
    	
    	COMMENT INPUT/OUTPUT SECTION:
    	
    		-- None
    
    	COMMENT OUTPUT SECTION:
    		
    		--Sets CPACR of SCB value.	
    		VAR SCB->CPACR,						init = 0x100,					ev = 0xF00100
    		
    		--Sets CR of RCC value.
    		VAR RCC->CR,						init= 0x82,						ev = 0x83
    		
    		--Sets CFGR of RCC value.
    		VAR RCC->CFGR,						init = 0x0,						ev = HEX_ZERO
    		
    		--Sets PLLCFGR of RCC value.
    		VAR RCC->PLLCFGR,					init = 0x24003010,				ev = HEX_PLLCFGR
    		
    		--Sets CIR of RCC value.
    		VAR RCC->CIR,						init = 0x0,						ev = HEX_ZERO
    		
    		--Sets VTOR of RCC value.
			VAR SCB->VTOR,						init = 0x10,					ev = init
    
    	-- Stub Calls
    	SIMUL
    	
    		-- No Stubs
    	
    	END SIMUL
    
    -- Calling the Unit under test
    #SystemInit();
    END ELEMENT

END TEST -- TEST 3

END SERVICE -- SystemInit


