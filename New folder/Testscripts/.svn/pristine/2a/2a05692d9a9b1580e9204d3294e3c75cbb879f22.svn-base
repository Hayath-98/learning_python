HEADER hdulibstm32f4xxfsmc,Source Code Ver : 1.2,TestScript Ver : 1.3
--------------------------------------------------------------------
-- Author Name         : Swathi G
--
-- File Name           : hdulibstm32f4xxfsmc.ptu
--
-- Test Creation Date  : 08.09.2017 
--
-- Tested CSUs         : FsmcNorSramInit
--                       FsmcNorSramCmd
--
-- Version History     : 
--
-- TEST-VERSION      	TEST-DATE   	TESTER NAME  	     MODIFICATIONS
--  --------            ------           --------            ---------
--  1.1                 08.09.2017       Swathi G            Created the PTU file.
--  1.2                 10.11.2017       Swathi G            Updated as per #PR100171.
--  1.3                 14.11.2017       Swathi G            Updated as per #PR100171.
--------------------------------------------------------------------------------
--
-- TESTED FILE INCLUDE DIRECTIVES 
--
--------------------------------------------------------------------------------

#/*============================================================================*/
#/* Module specific includes                                                   */
#/*============================================================================*/

##include "hdulibbasicfunc.h"
##include "hdulibstm32f4xx.h"
##include "hdulibstm32f4xxfsmc.h"
--------------------------------------------------------------------------------
--
-- TESTED FILE DECLARATIONS 
--
--------------------------------------------------------------------------------

-- Declarations of the global variables of the tested file

    --None

-- Declarations of the temporary variables of the tested file

    --Temporary variable to hold the value of the structure variable T_FSMC_NORSRAMTIMINGINIT
    # T_FSMC_NORSRAM_TIMING_INIT PTU_FSMC_NORSRAM_TIMING_INIT;

--------------------------------------------------------------------------------

BEGIN

    COMMENT Author Name      : Swathi G
    COMMENT
    COMMENT File Name        : hdulibstm32f4xxfsmc.ptu
    COMMENT
    COMMENT Tested Functions : FsmcNorSramInit
    COMMENT                    FsmcNorSramCmd
    COMMENT
    COMMENT Assumptions and Constraints
    COMMENT <AC>
    COMMENT Assumption(s):
    COMMENT None 
    COMMENT 
    COMMENT Constraint(s):
    COMMENT 
    COMMENT 1.Header file 'hdulibstm32f4xx.h' is modified for testing purpose.
    COMMENT   The macro FSMC_BANK1 is modified for stubbing hardware address: 
    COMMENT   /*Commented for testing purpose*/
    COMMENT   /*#define FSMC_BANK1            ((T_FSMC_BANK1 *) FSMC_BANK1_R_BASE)*/
    COMMENT 
    COMMENT   /* Modified macro by stubbing hardware address for testing purpose */
    COMMENT   T_FSMC_BANK1   PTU_FSMC_BANK1;
    COMMENT   #define FSMC_BANK1   ((T_FSMC_BANK1 *)& PTU_FSMC_BANK1)
    COMMENT
    COMMENT 2.Header file 'hdulibstm32f4xx.h' is modified for testing purpose.
    COMMENT   The macro FSMC_BANK1E is modified for stubbing hardware address: 
    COMMENT   /*Commented for testing purpose*/
    COMMENT   /*#define FSMC_BANK1E         ((T_FSMC_BANK1E *) FSMC_BANK1E_R_BASE)*/
    COMMENT 
    COMMENT   /* Modified macro by stubbing hardware address for testing purpose */
    COMMENT   T_FSMC_BANK1E   PTU_FSMC_BANK1E;
    COMMENT   #define FSMC_BANK1E  ((T_FSMC_BANK1E *)& PTU_FSMC_BANK1E) 
    COMMENT </AC>

    COMMENT Equivalence Class Description
    COMMENT <ECD>
    COMMENT 
    COMMENT Valid Class(es)
    COMMENT Class 1
    COMMENT The function 'FsmcNorSramInit',initializes the FSMC NOR/SRAM Banks.
    COMMENT 
    COMMENT Class 2
    COMMENT In function 'FsmcNorSramCmd',new_state is equal to ENABLE.
    COMMENT 
    COMMENT Class 3
    COMMENT In function 'FsmcNorSramCmd',new_state is equal to DISABLE.
    COMMENT 
    COMMENT Invalid Class(es)
    COMMENT Class 4
    COMMENT In function 'FsmcNorSramInit',the address of the parameter 'fsmc_norsraminit_struct' is NULL.
    COMMENT </ECD>

--------------------------------------------------------------------------------
--
-- STUB DECLARATIONS OF THE TESTED OR INTEGRATED UNITS
--
--------------------------------------------------------------------------------

SIMUL

     DEFINE STUB hdulibstm32f4xxfsmc_c

           -- No stubs

     END DEFINE

ELSE_SIMUL

    -- No action defined as of now if the module is not simulated

END SIMUL

--------------------------------------------------------------------------------
--
-- ENVIRONMENTS DECLARATIONS
--
--------------------------------------------------------------------------------
--IF HOST

-- No special things that have to be done on the host to run the test.

--ELSE

-- No action defined for the target execution as of now

--END IF
--------------------------------------------------------------------------------
--------------------------------------------------------------------------------
-- TESTED FILE ENVIRONMENT
--------------------------------------------------------------------------------

--Environment of the file hdulibstm32f4xxfsmc.c

--ENVIRONMENT ENV_hdulibstm32f4xxfsmc_c

    --None

--END ENVIRONMENT --ENV_hdulibstm32f4xxfsmc_c

--USE   ENV_hdulibstm32f4xxfsmc_c

--------------------------------------------------------------------------------
--
--  SERVICES DECLARATIONS
--
--------------------------------------------------------------------------------

--------------------------------------------------------------------------------
-- Declaration of the service    FsmcNorSramInit
--------------------------------------------------------------------------------
SERVICE FsmcNorSramInit
SERVICE_TYPE extern

-- Holds the details to initialize FSMC NORSRAM
# T_FSMC_NORSRAM_INIT  fsmc_norsraminit_struct;

-- Default Variables 
ENVIRONMENT ENV_FsmcNorSramInit

    --Pointer variable that contains the configuration information for the FSMC NOR/SRAM specified Banks.
    STRUCTURE fsmc_norsraminit_struct,       init = { fsmc_bank =>RESET, 
    &                                                    fsmc_data_address_mux =>RESET, 
    &                                                    fsmc_memory_type =>RESET, 
    &                                                    fsmc_memory_datawidth =>RESET,
    &                                                    fsmc_burst_accessmode =>RESET,
    &                                                    fsmc_asynchronous_wait =>RESET, 
    &                                                    fsmc_waitsignal_polarity =>RESET,
    &                                                    fsmc_wrap_mode =>RESET,
    &                                                    fsmc_waitsignal_active =>RESET,
    &                                                    fsmc_write_operation =>RESET, 
    &                                                    fsmc_waitsignal =>RESET, 
    &                                                    fsmc_extended_mode =>RESET,
    &                                                    fsmc_write_burst =>RESET, 
    &                                                    fsmc_readwrite_timing_struct =>NULL,
    &                                                    fsmc_write_timing_struct =>NULL  },
    &                                                                                       ev = init 

    --Temporary variable holds the value of T_FSMC_NORSRAM_TIMING_INIT's member whose address is passed to members
    --fsmc_readwrite_timing_struct and fsmc_write_timing_struct of fsmc_norsraminit_struct.
    STRUCTURE PTU_FSMC_NORSRAM_TIMING_INIT, init = { fsmc_address_setuptime =>RESET,
    &                                                    fsmc_address_holdtime =>RESET,
    &                                                    fsmc_data_setuptime =>RESET,
    &                                                    fsmc_bus_turnaround_duration =>RESET,
    &                                                    fsmc_clk_division =>RESET,
    &                                                    fsmc_data_latency =>RESET,
    &                                                    fsmc_access_mode =>RESET },
    &                                                                                       ev = init

    --FSMC BANK1 NOR/SRAM BTR and BCR register 
    ARRAY FSMC_BANK1->BTCR,                 init = { others =>RESET },                      ev = init

    --Bank1 NOR/SRAM timing register
    ARRAY FSMC_BANK1E->BWTR,                init = { others =>RESET },                      ev = init 

END ENVIRONMENT --ENV_FsmcNorSramInit

USE ENV_FsmcNorSramInit

TEST 1
FAMILY nominal

    COMMENT Test Description
    COMMENT <TD> 
    COMMENT [hdulibstm32f4xxfsmc-FsmcNorSramInit-CB-LLR-001] 
    COMMENT The function FsmcNorSramInit performs following 
    COMMENT 1.The BANK1 NOR/SRAM control register (BCR) is configured by initializing the
    COMMENT received configuration information for members as follows 
    COMMENT 1.1 fsmc_bank with value FSMC_BANK1_NORSRAM1,
    COMMENT 1.2 fsmc_data_address_mux with value FSMC_DATAADDRESSMUX_DISABLE,
    COMMENT 1.3 fsmc_memory_type with value FSMC_MEMORYTYPE_PSRAM,
    COMMENT 1.4 fsmc_memory_datawidth with value FSMC_MEMORYDATAWIDTH_16B,
    COMMENT 1.5 fsmc_burst_accessmode with value FSMC_BURSTACCESSMODE_DISABLE,
    COMMENT 1.6 fsmc_asynchronous_wait with value FSMC_ASYNCHRONOUSWAIT_DISABLE,
    COMMENT 1.7 fsmc_waitsignal_polarity with value FSMC_WAITSIGNALPOLARITY_LOW,
    COMMENT 1.8 fsmc_wrap_mode with value FSMC_WRAPMODE_DISABLE,
    COMMENT 1.9 fsmc_waitsignal_active with value FSMC_WAIT_SIG_ACTIVE_BEF_WAIT,
    COMMENT 1.10 fsmc_write_operation with value FSMC_WRITEOPERATION_ENABLE,
    COMMENT 1.11 fsmc_waitsignal with value FSMC_WAITSIGNAL_DISABLE,
    COMMENT 1.12 fsmc_extended_mode with value FSMC_EXTENDEDMODE_DISABLE,
    COMMENT 1.13 fsmc_write_burst with value FSMC_WRITEBURST_DISABLE.
    COMMENT Equivalence Class 1
    COMMENT
    COMMENT [hdulibstm32f4xxfsmc-FsmcNorSramInit-CB-LLR-002]
    COMMENT The Bank1 NOR/SRAM timing register (BTR) is initialised with the received  
    COMMENT Timing parameters(fsmc_norsraminit_struct parameter tested with minimum values)
    COMMENT Equivalence Class None
    COMMENT
    COMMENT [hdulibstm32f4xxfsmc-FsmcNorSramInit-CB-LLR-003]
    COMMENT The Bank1 NOR/SRAM timing register (BWTR) of fsmc_norsraminit_struct of fsmc_bank 
    COMMENT is configured with the value BWTR_RESET_VALUE.      
    COMMENT Equivalence Class None
    COMMENT </TD>

    ELEMENT

        COMMENT INPUT SECTION:

            --Temporary variable holds the value of T_FSMC_NORSRAM_TIMING_INIT's member whose address is passed to members
            --fsmc_readwrite_timing_struct and fsmc_write_timing_struct of fsmc_norsraminit_struct.
            STRUCTURE PTU_FSMC_NORSRAM_TIMING_INIT,     init = { fsmc_address_setuptime =>0x0,
            &                                                    fsmc_address_holdtime =>0x0, 
            &                                                    fsmc_data_setuptime =>0x0,
            &                                                    fsmc_bus_turnaround_duration =>0x0,
            &                                                    fsmc_clk_division =>0x1, 
            &                                                    fsmc_data_latency =>0x0,
            &                                                    fsmc_access_mode =>FSMC_ACCESSMODE_A },
            &                                                                               ev = init

            --Pointer variable that contains the configuration information for the FSMC NOR/SRAM specified Banks.
            STRUCTURE fsmc_norsraminit_struct,          init = { fsmc_bank =>FSMC_BANK1_NORSRAM1,
            &                                                    fsmc_data_address_mux =>FSMC_DATAADDRESSMUX_DISABLE,
            &                                                    fsmc_memory_type =>FSMC_MEMORYTYPE_PSRAM,
            &                                                    fsmc_memory_datawidth =>FSMC_MEMORYDATAWIDTH_16B,
            &                                                    fsmc_burst_accessmode =>FSMC_BURSTACCESSMODE_DISABLE, 
            &                                                    fsmc_asynchronous_wait =>FSMC_ASYNCHRONOUSWAIT_DISABLE,
            &                                                    fsmc_waitsignal_polarity =>FSMC_WAITSIGNALPOLARITY_LOW,
            &                                                    fsmc_wrap_mode =>FSMC_WRAPMODE_DISABLE,
            &                                                    fsmc_waitsignal_active =>FSMC_WAIT_SIG_ACTIVE_BEF_WAIT,
            &                                                    fsmc_write_operation =>FSMC_WRITEOPERATION_ENABLE,
            &                                                    fsmc_waitsignal =>FSMC_WAITSIGNAL_DISABLE,
            &                                                    fsmc_extended_mode =>FSMC_EXTENDEDMODE_DISABLE,
            &                                                    fsmc_write_burst =>FSMC_WRITEBURST_DISABLE,
            &                                                    fsmc_readwrite_timing_struct =>&PTU_FSMC_NORSRAM_TIMING_INIT, 
            &                                                    fsmc_write_timing_struct =>&PTU_FSMC_NORSRAM_TIMING_INIT },
            &                                                                               ev = init

        COMMENT INPUT/OUTPUT SECTION:

            --None

        COMMENT OUTPUT SECTION:

            --FSMC BANK1 NOR/SRAM BTR and BCR register 
            ARRAY FSMC_BANK1->BTCR,                   init = { others =>RESET },          ev = { 0 =>0x1014, 1 =>0x100000, others =>RESET }

            --Bank1 NOR/SRAM timing register
            ARRAY FSMC_BANK1E->BWTR,                  init = { others =>RESET },          ev = { 0 =>0x0FFFFFFF, others =>RESET }

        -- Stub Calls:
        SIMUL

            --No Stubs

        END SIMUL

        -- Calling the Unit Under Test:
        #FsmcNorSramInit(&fsmc_norsraminit_struct);

    END ELEMENT
    
END TEST  -- TEST 1

TEST 2
FAMILY nominal

    COMMENT Test Description
    COMMENT <TD> 
    COMMENT [hdulibstm32f4xxfsmc-FsmcNorSramInit-CB-LLR-001] 
    COMMENT The function FsmcNorSramInit performs following 
    COMMENT 1.The BANK1 NOR/SRAM control register (BCR) is configured by initializing the
    COMMENT received configuration information for members such as 
    COMMENT 1.1 fsmc_bank with value FSMC_BANK1_NORSRAM2,
    COMMENT 1.2 fsmc_data_address_mux with value FSMC_DATAADDRESSMUX_DISABLE,
    COMMENT 1.3 fsmc_memory_type with value FSMC_MEMORYTYPE_PSRAM,
    COMMENT 1.4 fsmc_memory_datawidth with value FSMC_MEMORYDATAWIDTH_16B,
    COMMENT 1.5 fsmc_burst_accessmode with value FSMC_BURSTACCESSMODE_DISABLE,
    COMMENT 1.6 fsmc_asynchronous_wait with value FSMC_ASYNCHRONOUSWAIT_DISABLE,
    COMMENT 1.7 fsmc_waitsignal_polarity with value FSMC_WAITSIGNALPOLARITY_LOW,
    COMMENT 1.8 fsmc_wrap_mode with value FSMC_WRAPMODE_DISABLE,
    COMMENT 1.9 fsmc_waitsignal_active with value FSMC_WAIT_SIG_ACTIVE_BEF_WAIT,
    COMMENT 1.10 fsmc_write_operation with value FSMC_WRITEOPERATION_ENABLE,
    COMMENT 1.11 fsmc_waitsignal with value FSMC_WAITSIGNAL_DISABLE,
    COMMENT 1.12 fsmc_extended_mode with value FSMC_EXTENDEDMODE_DISABLE,
    COMMENT 1.13 fsmc_write_burst with value FSMC_WRITEBURST_DISABLE.
    COMMENT Equivalence Class 1
    COMMENT
    COMMENT [hdulibstm32f4xxfsmc-FsmcNorSramInit-CB-LLR-002]
    COMMENT The Bank1 NOR/SRAM timing register (BTR) is initialised with the received  
    COMMENT Timing parameters(fsmc_norsraminit_struct parameter tested with maximum values)
    COMMENT Equivalence Class None
    COMMENT
    COMMENT [hdulibstm32f4xxfsmc-FsmcNorSramInit-CB-LLR-003]
    COMMENT The Bank1 NOR/SRAM timing register (BWTR) of fsmc_norsraminit_struct of fsmc_bank 
    COMMENT is configured with the value BWTR_RESET_VALUE.
    COMMENT Equivalence Class None
    COMMENT </TD>

    ELEMENT

        COMMENT INPUT SECTION:

            --Temporary variable holds the value of T_FSMC_NORSRAM_TIMING_INIT's member whose address is passed to members
            --fsmc_readwrite_timing_struct and fsmc_write_timing_struct of fsmc_norsraminit_struct.
            STRUCTURE PTU_FSMC_NORSRAM_TIMING_INIT,     init = { fsmc_address_setuptime =>0xF,
            &                                                    fsmc_address_holdtime =>0xF,
            &                                                    fsmc_data_setuptime =>0xFF,
            &                                                    fsmc_bus_turnaround_duration =>0xF,
            &                                                    fsmc_clk_division =>0xF,
            &                                                    fsmc_data_latency =>0xF,
            &                                                    fsmc_access_mode =>0x10000000 },
            &                                                                               ev = init

            --Pointer variable that contains the configuration information for the FSMC NOR/SRAM specified Banks.
            STRUCTURE fsmc_norsraminit_struct,          init = { fsmc_bank =>FSMC_BANK1_NORSRAM2,
            &                                                    fsmc_data_address_mux =>FSMC_DATAADDRESSMUX_DISABLE,
            &                                                    fsmc_memory_type =>FSMC_MEMORYTYPE_PSRAM,
            &                                                    fsmc_memory_datawidth =>FSMC_MEMORYDATAWIDTH_16B,
            &                                                    fsmc_burst_accessmode =>FSMC_BURSTACCESSMODE_DISABLE, 
            &                                                    fsmc_asynchronous_wait =>FSMC_ASYNCHRONOUSWAIT_DISABLE,
            &                                                    fsmc_waitsignal_polarity =>FSMC_WAITSIGNALPOLARITY_LOW,
            &                                                    fsmc_wrap_mode =>FSMC_WRAPMODE_DISABLE,
            &                                                    fsmc_waitsignal_active =>FSMC_WAIT_SIG_ACTIVE_BEF_WAIT,
            &                                                    fsmc_write_operation =>FSMC_WRITEOPERATION_ENABLE,
            &                                                    fsmc_waitsignal =>FSMC_WAITSIGNAL_DISABLE,
            &                                                    fsmc_extended_mode =>FSMC_EXTENDEDMODE_DISABLE,
            &                                                    fsmc_write_burst =>FSMC_WRITEBURST_DISABLE,
            &                                                    fsmc_readwrite_timing_struct =>&PTU_FSMC_NORSRAM_TIMING_INIT, 
            &                                                    fsmc_write_timing_struct =>&PTU_FSMC_NORSRAM_TIMING_INIT },
            &                                                                               ev = init

        COMMENT INPUT/OUTPUT SECTION:

            --None

        COMMENT OUTPUT SECTION:

            --FSMC BANK1 NOR/SRAM BTR and BCR register 
            ARRAY FSMC_BANK1->BTCR,                   init = { others =>RESET },          ev = { 2 =>0x1014,3 =>0x1FFFFFFF, others =>RESET }

            --Bank1 NOR/SRAM timing register
            ARRAY FSMC_BANK1E->BWTR,                  init = { others =>RESET },          ev = { 2 =>0x0FFFFFFF, others =>RESET }
            
        -- Stub Calls :
        SIMUL

            --No Stubs

        END SIMUL

        -- Calling the Unit Under Test :
        #FsmcNorSramInit(&fsmc_norsraminit_struct);

    END ELEMENT

END TEST  -- TEST 2

TEST 3
FAMILY nominal

    COMMENT Test Description
    COMMENT <TD> 
    COMMENT [hdulibstm32f4xxfsmc-FsmcNorSramInit-CB-LLR-001] 
    COMMENT The function FsmcNorSramInit performs following 
    COMMENT 1.The BANK1 NOR/SRAM control register (BCR) is configured by initializing the
    COMMENT received configuration information for members such as 
    COMMENT 1.1 fsmc_bank with value FSMC_BANK1_NORSRAM2,
    COMMENT 1.2 fsmc_data_address_mux with value 0x00000001,
    COMMENT 1.3 fsmc_memory_type with value 0x00000000,
    COMMENT 1.4 fsmc_memory_datawidth with value 0x00000000,
    COMMENT 1.5 fsmc_burst_accessmode with value 0x00000080,
    COMMENT 1.6 fsmc_asynchronous_wait with value 0x00008000,
    COMMENT 1.7 fsmc_waitsignal_polarity with value 0x00000200,
    COMMENT 1.8 fsmc_wrap_mode with value 0x00000400,
    COMMENT 1.9 fsmc_waitsignal_active with value 0x00000800,
    COMMENT 1.10 fsmc_write_operation with value 0x00001000,
    COMMENT 1.11 fsmc_waitsignal with value 0x00002000,
    COMMENT 1.12 fsmc_extended_mode with value FSMC_EXTENDEDMODE_ENABLE,
    COMMENT 1.13 fsmc_write_burst with value 0x00040000.
    COMMENT Equivalence Class 1
    COMMENT
    COMMENT [hdulibstm32f4xxfsmc-FsmcNorSramInit-CB-LLR-002]
    COMMENT The Bank1 NOR/SRAM timing register (BTR) is initialised with the received  
    COMMENT Timing parameters(fsmc_norsraminit_struct parameter tested with nominal values)
    COMMENT Equivalence Class None
    COMMENT
    COMMENT [hdulibstm32f4xxfsmc-FsmcNorSramInit-CB-LLR-003]
    COMMENT The Bank1 NOR/SRAM timing register (BWTR) of fsmc_norsraminit_struct of fsmc_bank 
    COMMENT is configured with the value BWTR_RESET_VALUE.
    COMMENT Equivalence Class None
    COMMENT </TD>

    ELEMENT

        COMMENT INPUT SECTION:

            --Temporary variable holds the value of T_FSMC_NORSRAM_TIMING_INIT's member whose address is passed to members
            --fsmc_readwrite_timing_struct and fsmc_write_timing_struct of fsmc_norsraminit_struct.
            STRUCTURE PTU_FSMC_NORSRAM_TIMING_INIT,    init = { fsmc_address_setuptime =>0x7,
            &                                                   fsmc_address_holdtime =>0x7,
            &                                                   fsmc_data_setuptime =>0x7F,
            &                                                   fsmc_bus_turnaround_duration =>0x7,
            &                                                   fsmc_clk_division =>0x7,
            &                                                   fsmc_data_latency =>0x7,
            &                                                   fsmc_access_mode =>0x18000000 },
            &                                                                               ev = init

            --Pointer variable that contains the configuration information for the FSMC NOR/SRAM specified Banks.
            STRUCTURE fsmc_norsraminit_struct,          init = { fsmc_bank =>FSMC_BANK1_NORSRAM2,
            &                                                    fsmc_data_address_mux =>(T_UINT32)0x00000001,
            &                                                    fsmc_memory_type =>(T_UINT32)0x00000000,
            &                                                    fsmc_memory_datawidth =>(T_UINT32)0x00000000,
            &                                                    fsmc_burst_accessmode =>(T_UINT32)0x00000080, 
            &                                                    fsmc_asynchronous_wait =>(T_UINT32)0x00008000,
            &                                                    fsmc_waitsignal_polarity =>(T_UINT32)0x00000200,
            &                                                    fsmc_wrap_mode =>(T_UINT32)0x00000400,
            &                                                    fsmc_waitsignal_active =>(T_UINT32)0x00000800,
            &                                                    fsmc_write_operation =>(T_UINT32)0x00001000,
            &                                                    fsmc_waitsignal =>(T_UINT32)0x00002000,
            &                                                    fsmc_extended_mode =>FSMC_EXTENDEDMODE_ENABLE,
            &                                                    fsmc_write_burst =>(T_UINT32)0x00040000,
            &                                                    fsmc_readwrite_timing_struct =>&PTU_FSMC_NORSRAM_TIMING_INIT, 
            &                                                    fsmc_write_timing_struct =>&PTU_FSMC_NORSRAM_TIMING_INIT },
            &                                                                               ev = init

        COMMENT INPUT/OUTPUT SECTION:
        
            --None

        COMMENT OUTPUT SECTION:

            --FSMC BANK1 NOR/SRAM BTR and BCR register 
            ARRAY FSMC_BANK1->BTCR,                    init = { others =>RESET },         ev = { 2 =>0x4FE81,3 =>0x1F777F77, others =>RESET }

            --Bank1 NOR/SRAM timing register
            ARRAY FSMC_BANK1E->BWTR,                  init = { others =>RESET },          ev = { 2 =>0x0FFFFFFF, others =>RESET }

        -- Stub Calls :
        SIMUL

            --No Stubs

        END SIMUL

        -- Calling the Unit Under Test :
        #FsmcNorSramInit(&fsmc_norsraminit_struct);

    END ELEMENT

END TEST  -- TEST 3

--TEST 4
--FAMILY robustness
--
--  COMMENT Test Description
--  COMMENT <TD> 
--  COMMENT [hdulibstm32f4xxfsmc-FsmcNorSramInit-CB-LLR-001]
--  COMMENT Function is tested by passing Invalid Reference to function
--  COMMENT parameter 'fsmc_norsraminit_struct'.
--  COMMENT Equivalence Class 4
--  COMMENT </TD>
--  
--  ELEMENT
--
--      COMMENT INPUT SECTION:
--
--          --None
--
--      COMMENT INPUT/OUTPUT SECTION:
--
--          --None
--
--      COMMENT OUTPUT SECTION:
--
--          --None
--
--      -- Stub Calls : 
--      SIMUL
--
--          --No Stubs
--
--      END SIMUL
--
--      -- Calling the Unit Under Test :
--      #FsmcNorSramInit(NULL);
--
--  END ELEMENT
-- 
--END TEST  -- TEST 4

END SERVICE --FsmcNorSramInit
--------------------------------------------------------------------------------
-- Declaration of the service    FsmcNorSramCmd
--------------------------------------------------------------------------------
SERVICE FsmcNorSramCmd
SERVICE_TYPE extern

-- Holds the FSMC NOR/SRAM bank name
# T_UINT32  fsmc_bank;
-- Holds the FSMC functional state value
# T_FUNCTIONAL_STATE  new_state;

-- Default Variables 
ENVIRONMENT ENV_FsmcNorSramCmd

    --Input parameter specifies the FSMC Bank to be used.
    VAR fsmc_bank,                  init = RESET,                       ev = init

    --Input parameter specifies the state of the fsmc_bank.
    VAR new_state,                  init = RESET,                       ev = init

    --FSMC BANK1 NOR/SRAM BTR and BCR register 
    ARRAY FSMC_BANK1->BTCR,         init = { others =>RESET },          ev = init

END ENVIRONMENT --ENV_FsmcNorSramCmd

USE ENV_FsmcNorSramCmd

TEST 1
FAMILY nominal

    COMMENT Test Description
    COMMENT <TD> 
    COMMENT [hdulibstm32f4xxfsmc-FsmcNorSramCmd-CB-LLR-002]
    COMMENT 1.The function 'FsmcNorSramCmd' disables the selected NOR/SRAM Bank by clearing
    COMMENT the MBKEN bit in the BCRx register when new_state of FSMC bank is DISABLE.
    COMMENT 2.Parameter fsmc_bank tested for value FSMC_BANK1_NORSRAM1.
    COMMENT Equivalence Class 3
    COMMENT </TD>

    ELEMENT

        COMMENT INPUT SECTION:

            --Input parameter specifies the FSMC Bank to be used.
            VAR fsmc_bank,                  init = FSMC_BANK1_NORSRAM1,         ev = init

            --Input parameter specifies the state of the fsmc_bank.
            VAR new_state,                  init = DISABLE,                     ev = init 

        COMMENT INPUT/OUTPUT SECTION:
        
            --FSMC BANK1 NOR/SRAM BTR and BCR register 
            ARRAY FSMC_BANK1->BTCR,         init = { others =>0xF0F1 },         ev ={ 0 =>0xF0F0 , others =>0xF0F1 }

        COMMENT OUTPUT SECTION:

            --None

        -- Stub Calls :
        SIMUL

            --No Stubs

        END SIMUL

        -- Calling the Unit Under Test :
        #FsmcNorSramCmd(fsmc_bank,new_state);

    END ELEMENT

END TEST  -- TEST 1

TEST 2
FAMILY nominal

    COMMENT Test Description
    COMMENT <TD> 
    COMMENT [hdulibstm32f4xxfsmc-FsmcNorSramCmd-CB-LLR-001]
    COMMENT 1.The function 'FsmcNorSramCmd' enables the selected NOR/SRAM Bank by setting
    COMMENT the MBKEN bit in the BCRx register when new_state of FSMC bank is ENABLE.
    COMMENT 2.Parameter fsmc_bank tested for value FSMC_BANK1_NORSRAM1.
    COMMENT Equivalence Class 2
    COMMENT </TD>

    ELEMENT

        COMMENT INPUT SECTION:

            --Input parameter specifies the FSMC Bank to be used.
            VAR fsmc_bank,                  init = FSMC_BANK1_NORSRAM1,             ev = init   

            --Input parameter specifies the state of the fsmc_bank.
            VAR new_state,                  init = ENABLE,                          ev = init 

        COMMENT INPUT/OUTPUT SECTION:
        
            --FSMC BANK1 NOR/SRAM BTR and BCR register 
            ARRAY FSMC_BANK1->BTCR,         init = { others =>0xF0F0 },             ev = { 0 =>0xF0F1,others =>0xF0F0 }

        COMMENT OUTPUT SECTION:

            --None

        -- Stub Calls :
        SIMUL

            --No Stubs

        END SIMUL

        -- Calling the Unit Under Test :
        #FsmcNorSramCmd(fsmc_bank,new_state);

    END ELEMENT
   
END TEST  -- TEST 2

TEST 3
FAMILY nominal

    COMMENT Test Description
    COMMENT <TD> 
    COMMENT [hdulibstm32f4xxfsmc-FsmcNorSramCmd-CB-LLR-002]
    COMMENT 1.The function 'FsmcNorSramCmd' disables the selected NOR/SRAM Bank by clearing
    COMMENT the MBKEN bit in the BCRx register when new_state of FSMC bank is DISABLE.
    COMMENT 2.Parameter fsmc_bank tested for value FSMC_BANK1_NORSRAM2.
    COMMENT Equivalence Class 3
    COMMENT </TD>

    ELEMENT

        COMMENT INPUT SECTION:

            --Input parameter specifies the FSMC Bank to be used.
            VAR fsmc_bank,                  init = FSMC_BANK1_NORSRAM2,             ev = init   

            --Input parameter specifies the state of the fsmc_bank.
            VAR new_state,                  init = DISABLE,                         ev = init 

        COMMENT INPUT/OUTPUT SECTION:

            --FSMC BANK1 NOR/SRAM BTR and BCR register 
            ARRAY FSMC_BANK1->BTCR,         init = { others =>0xFFFF },             ev = { 2 =>0xFFFE , others =>0xFFFF }

        COMMENT OUTPUT SECTION:

            --None

        -- Stub Calls :
        SIMUL

            --No Stubs

        END SIMUL

        -- Calling the Unit Under Test :
        #FsmcNorSramCmd(fsmc_bank,new_state);

    END ELEMENT

END TEST  -- TEST 3

TEST 4
FAMILY nominal

    COMMENT Test Description
    COMMENT <TD> 
    COMMENT [hdulibstm32f4xxfsmc-FsmcNorSramCmd-CB-LLR-001]
    COMMENT 1.The function 'FsmcNorSramCmd' enables the selected NOR/SRAM Bank by setting
    COMMENT the MBKEN bit in the BCRx register when new_state of FSMC bank is ENABLE.
    COMMENT 2.Parameter fsmc_bank tested for value FSMC_BANK1_NORSRAM2.
    COMMENT Equivalence Class 2
    COMMENT </TD>

    ELEMENT

        COMMENT INPUT SECTION:

            --Input parameter specifies the FSMC Bank to be used.
            VAR fsmc_bank,                  init = FSMC_BANK1_NORSRAM2,         ev = init

            --Input parameter specifies the state of the fsmc_bank.
            VAR new_state,                  init = ENABLE,                      ev = init 

        COMMENT INPUT/OUTPUT SECTION:

            --FSMC BANK1 NOR/SRAM BTR and BCR register 
            ARRAY FSMC_BANK1->BTCR,        init = { others =>0xFFFE },          ev = { 2 =>0xFFFF, others =>0xFFFE }

        COMMENT OUTPUT SECTION:

            --None

        -- Stub Calls :
        SIMUL

            --No Stubs

        END SIMUL

        -- Calling the Unit Under Test :
        #FsmcNorSramCmd(fsmc_bank,new_state);

    END ELEMENT

END TEST  -- TEST 4

END SERVICE --FsmcNorSramCmd